plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    id 'com.gradleup.nmcp' version '1.3.0'
    alias(libs.plugins.java.platform)
    alias(libs.plugins.maven.publish)
    id 'version-catalog'
    id 'signing'
}

scmVersion {
    tag {
        prefix = 'v'
        initialVersion { config, position -> '0.4.0' }
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

group = 'ai.pipestream'
version = scmVersion.version
description = 'Pipeline BOM - Bill of Materials'

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform(libs.quarkus.bom)
    api platform("io.grpc:grpc-bom:${libs.versions.grpc.get()}")
    api platform("com.google.protobuf:protobuf-bom:${libs.versions.protobuf.get()}")

    constraints {
        api libs.opensearch.java
        api libs.wiremock.standalone
        api libs.wiremock.grpc.extension.standalone
        api libs.apicurio.registry.java.sdk
        api libs.apicurio.registry.common
        api libs.apicurio.registry.protobuf.serde
        api libs.commonmark
        api libs.pdfbox
        api libs.password4j
        api libs.quarkus.mcp.server.sse
        api libs.smallrye.reactive.messaging.in.memory

        api("io.grpc:grpc-protobuf") { version { strictly(libs.versions.grpc.get()) } }
        api("io.grpc:grpc-stub") { version { strictly(libs.versions.grpc.get()) } }
        api("io.grpc:grpc-api") { version { strictly(libs.versions.grpc.get()) } }
        api("com.google.protobuf:protobuf-java") { version { strictly(libs.versions.protobuf.get()) } }
        api("com.google.protobuf:protobuf-java-util") { version { strictly(libs.versions.protobuf.get()) } }
        api("com.google.protobuf:protoc") { version { strictly(libs.versions.protobuf.get()) } }

        // Internal ai.pipestream dependencies - all in coordinated release train
        api("ai.pipestream:devservices:${version}")
        api("ai.pipestream:devservices-deployment:${version}")

        // grpc modules - now part of release train
        api("ai.pipestream:grpc-stubs:${version}")
        api("ai.pipestream:grpc-google-descriptor:${version}")

        // library modules - all in release train
        api("ai.pipestream:pipestream-wiremock-server:0.1.5")

        // Sample documents test data - separate release train
        api libs.pipestream.sample.doc.types
        api libs.pipestream.test.documents
        api libs.pipestream.parser.pipedoc.parsed
        api libs.pipestream.double.chunked.pipedocs
    }
}

catalog {
    versionCatalog {
        from(files("gradle/libs.versions.toml"))
    }
}

publishing {
    publications {
        create('maven', MavenPublication) {
            from components.javaPlatform
            artifactId = 'pipestream-bom'

            pom {
                name = 'Pipestream AI Pipeline BOM'
                description = 'Bill of Materials for the Pipestream AI platform'
                url = 'https://github.com/ai-pipestream/pipestream-bom'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'krickert'
                        name = 'Krickert'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/ai-pipestream/pipestream-bom.git'
                    developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-bom.git'
                    url = 'https://github.com/ai-pipestream/pipestream-bom'
                }
            }
        }

        create('catalog', MavenPublication) {
            from components.versionCatalog
            artifactId = 'pipestream-bom-catalog'

            pom {
                name = 'Pipestream AI Pipeline BOM Catalog'
                description = 'Version Catalog for the Pipestream AI platform'
                url = 'https://github.com/ai-pipestream/pipestream-bom'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'krickert'
                        name = 'Krickert'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/ai-pipestream/pipestream-bom.git'
                    developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-bom.git'
                    url = 'https://github.com/ai-pipestream/pipestream-bom'
                }
            }
        }
    }

    repositories {
        mavenLocal()
        
        // GitHub Packages
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/ai-pipestream/pipestream-bom')
            credentials {
                username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

// nmcp plugin provides publishAllPublicationsToCentralPortal and publishAllPublicationsToCentralPortalSnapshots tasks
// Configure credentials via environment variables: MAVEN_CENTRAL_USERNAME, MAVEN_CENTRAL_PASSWORD

signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        // Sign all Maven publications defined above
        sign publishing.publications
    }
}
